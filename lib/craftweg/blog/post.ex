defmodule Craftweg.Blog.Post do
  @moduledoc """
  This module is a struct that represents a blog post.
  """

  defstruct [:path, :slug, :old_slug, :title, :excerpt, :date, :categories, :body]

  @doc """
  This method takes the absolute path to the file representing a blog post,
  the contents of the file, and the attributes that have been generated by
  the parser and returns an instance of the Craftweg.Blog.Post struct.
  """
  @type attributes :: any
  @spec build(String.t(), attributes, String.t()) :: %Craftweg.Blog.Post{}
  def build(path, %{:excerpt => excerpt, "title" => title, "categories" => categories}, body) do
    filename_without_extension = path |> Path.rootname() |> Path.split() |> Enum.take(-1) |> hd
    [year, month, day] = filename_without_extension |> String.split("-") |> Enum.take(3)
    date = Date.from_iso8601!("#{year}-#{month}-#{day}")

    filename_without_date_and_extension =
      filename_without_extension |> String.replace("#{year}-#{month}-#{day}-", "")

    slug =
      "/blog/" <> year <> "/" <> month <> "/" <> day <> "/" <> filename_without_date_and_extension

    struct!(
      __MODULE__,
      path: path,
      old_slug: "/" <> filename_without_date_and_extension,
      slug: slug,
      title: title,
      date: date,
      body: body,
      categories: categories,
      excerpt: excerpt
    )
  end
end
